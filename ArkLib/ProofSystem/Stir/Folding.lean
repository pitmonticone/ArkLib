/-
Copyright (c) 2025 ArkLib Contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mirco Richter, Poulami Das (Least Authority)
-/

import ArkLib.Data.CodingTheory.ReedSolomon
import ArkLib.Data.CodingTheory.ListDecodability
import ArkLib.Data.Probability.Notation
import ArkLib.ProofSystem.Stir.ProximityBound

import Mathlib.Algebra.MvPolynomial.Basic
import Mathlib.Algebra.MvPolynomial.Degrees
import Mathlib.Probability.ProbabilityMassFunction.Basic
import Mathlib.Probability.Distributions.Uniform
import Mathlib.RingTheory.MvPolynomial.Groebner

/-! Section 4.4, [ACFY24stir]

## References

* [Arnon, G., Chiesa, A., Fenzi, G., and Yogev, E., *STIR: Reed-Solomon proximity testing
    with fewer queries*][ACFY24stir]
* [Sudan, M., *Reed-Solomon codes and polynomial reconstruction*][STIR2005]
* [Ben-Sasson, E. and Sudan, M., *Short PCPs with polylog query complexity*][BSS08]
-/

open Polynomial NNReal ReedSolomon LinearMap Finset ListDecodable STIR

namespace Domain

variable {Œπ F : Type*} [Field F] [Fintype F] [DecidableEq F] [DecidableEq Œπ]

/-- The image of a finite set `S` under the map `x ‚Ü¶ (œÜ x)·µè` -/
def indexPow (S : Finset Œπ) (œÜ : Œπ ‚Ü™ F) (k : ‚Ñï) : Finset F :=
  S.image (fun x => (œÜ x) ^ k)

/-- The k-th power domain `Œπ·µè ‚Ü™ F` for a given domain `Œπ ‚Ü™ F`. -/
def pow (S : Finset Œπ) (œÜ : Œπ ‚Ü™ F) (k : ‚Ñï) : indexPow S œÜ k ‚Ü™ F :=
    Function.Embedding.subtype fun y => y ‚àà indexPow S œÜ k

/-- The fiber over a point `y` under the map `x ‚Ü¶ (œÜ x)·µè` -/
def powFiber (S : Finset Œπ) (œÜ : Œπ ‚Ü™ F) (k : ‚Ñï) (y : indexPow S œÜ k) : Finset Œπ :=
  S.filter (fun x => (œÜ x) ^ k = y)

end Domain

namespace Folding

variable {F : Type*} [Field F] [Fintype F]

/- ùîΩ[X,Y] is not an Euclidean Domain, but fixing an order on monomials still allows
   to show existance of bivariate polynomials Q', Q ‚àà ùîΩ[X,Y] such that
   P = Q' * P' + Q for all P,P' ‚àà ùîΩ[X,Y] with P' having an invertible leading coefficient
   (which on a field is equivalent to P' not being the zero polynomial).

   This is MonomialOrder.div from Mathlib.RingTheory.MvPolynomial.Groebner

   Using the usual lexicographic order x‚ÇÄ > x‚ÇÅ is equal to proposition 6.3 in [BSS08]
   under the substitution z = x‚ÇÄ and y = x‚ÇÅ, hence the following definition constructs
   Q ‚àà ùîΩ[Z,Y] with P(z,y) = Q'(z,y) * R(z,y) + Q(z,y)

   Below we present Fact 4.6.1 from STIR -/

/-- Given `P, P' ‚àà ùîΩ[Z,Y]`, `P' ‚â† 0`, computes `Q ‚àà ùîΩ[Z,Y]`,
with `P(z,y) = Q'(z,y) * P'(z,y) + Q(z,y)` for some `Q' ‚àà ùîΩ[Z,Y]` -/
noncomputable def modBivar (P P' : MvPolynomial (Fin 2) F)
    (hlg : IsUnit ((MonomialOrder.lex).leadingCoeff P')) : MvPolynomial (Fin 2) F :=
      -- Lexicographic order on `Fin 2`.
      let ord : MonomialOrder (Fin 2) := MonomialOrder.lex
      -- Wrap the single divisor into a family indexed by `Unit`.
      let b : Unit ‚Üí MvPolynomial (Fin 2) F := fun _ => P'
      -- Unit leading-coeff proof for every index (there is only one).
      have hb : ‚àÄ i : Unit, IsUnit (ord.leadingCoeff (b i)) := by
        intro _; simpa [b, ord] using hlg
      -- Apply Groebner-basis division:
      -- hdiv : ‚àÉ Q', ‚àÉ Q, P =  P' * Q' + Q ‚àß (side conditions)
      have hdiv := ord.div (b := b) hb P
      -- Peel off the two nested existentials and return the chosen remainder `r`.
      Classical.choose (Classical.choose_spec hdiv)

/-- maps the univariate polynomial P‚ààùîΩ[Z] to the bivariate polynomial P'‚àà ùîΩ[Z,Y] with
    P'(z,y) = P(z) -/
noncomputable def uni2bi (p : Polynomial F) : MvPolynomial (Fin 2) F :=
  Polynomial.eval‚ÇÇ MvPolynomial.C (MvPolynomial.X 0) p

/-- Computes Q(z,y) with P(z) = Q'(z,y) * (y- q(z)) + Q(z,y) as in
    proposition 6.3 from [BSS08] -/
noncomputable def polyQ (P q : Polynomial F) : MvPolynomial (Fin 2) F :=
  -- Pbi(z,y):= P(z)
  let Pbi : MvPolynomial (Fin 2) F := uni2bi P
  -- P'(z,y) := (y - q(z))
  let P' : MvPolynomial (Fin 2) F := (MvPolynomial.X 1) - uni2bi q
  -- proof that leading coefficient f q is not zero
  have h_unit : IsUnit ((MonomialOrder.lex).leadingCoeff P') := by
    apply IsUnit.mk0
    rw [ne_eq, MonomialOrder.leadingCoeff_eq_zero_iff]
    intro h
    have h1 := sub_eq_zero.mp h
    have h2 := congr_arg (MvPolynomial.coeff (Finsupp.single 1 1)) h1
    simp only [uni2bi, MvPolynomial.coeff_X, ite_true] at h2
    suffices ‚àÄ r : Polynomial F, MvPolynomial.coeff (Finsupp.single 1 1)
        (Polynomial.eval‚ÇÇ (MvPolynomial.C (œÉ := Fin 2)) (MvPolynomial.X 0) r) = 0 by
      rw [this q] at h2; exact one_ne_zero h2
    intro r
    induction r using Polynomial.induction_on' with
    | add p q hp hq =>
      simp only [Polynomial.eval‚ÇÇ_add, MvPolynomial.coeff_add, hp, hq, add_zero]
    | monomial n a =>
      simp only [Polynomial.eval‚ÇÇ_monomial, MvPolynomial.coeff_C_mul,
        MvPolynomial.X_pow_eq_monomial, MvPolynomial.coeff_monomial]
      simp [Finsupp.single_eq_single_iff]
  modBivar Pbi P' h_unit

/-- Helper For Readability: Evaluate a bivariate polynomial Q at (a, b) ‚àà F√óF -/
def evalBivar
  (Q : MvPolynomial (Fin 2) F) (a b : F) : F := MvPolynomial.eval (Fin.cases a (fun _ ‚Ü¶ b)) Q

/-- The STIR paper assumes that the polynomials fPoly(.) and Q(qPoly(.),.) are
    fully determined by their evaluations on F. This is not necessarily true
    for arbitrary polynomials of degrees larger than |F|. So we include an
    assumption in what follows that qPoly has degree < |F| from which the
    uniqueness of fPoly and Q can be derived from their evaluation on F.
    Alternatively we could use the identity of polynomials
    fPoly(.) = Q(qPoly(.), .) instead.

    Below we present Fact 4.6.1 from STIR -/
lemma exists_unique_bivariate
  (qPoly : Polynomial F) (hdeg_q_min : qPoly.natDegree > 0)
  (hdeg_q_max : qPoly.natDegree < Fintype.card F) (fPoly : Polynomial F) :
    -- Q ‚àà ùîΩ[X,Y]
    ‚àÉ! Q : MvPolynomial (Fin 2) F,
      -- deg_x(Q) = Floor ( deg(fPoly) / deg(qPoly) )
      -- This is natural number division towards zero, which is floor
      (MvPolynomial.degreeOf 0 Q = (Polynomial.natDegree fPoly) / (Polynomial.natDegree qPoly)) ‚àß
      -- deg_y(Q) < deg (q)
      (MvPolynomial.degreeOf 1 Q < Polynomial.natDegree qPoly) ‚àß
      -- point‚Äëwise equality on F: f(z) = Q(q(z), z)
      (‚àÄ z : F, Polynomial.eval z fPoly = evalBivar Q (Polynomial.eval z qPoly) z) ‚àß
      (‚àÄ t : ‚Ñï, fPoly.natDegree < t * qPoly.natDegree ‚Üí MvPolynomial.degreeOf 0 Q < t) :=
  /- The proof can follow `def polyQ` using the properties guranteed
  from MonomialOrder.div from Mathlib.RingTheory.MvPolynomial.Groebner -/
  by sorry

/-- Fact 4.6.2 in STIR -/
lemma degree_bound_bivariate
  (qPoly : Polynomial F)
  (hdeg_q_min : qPoly.natDegree > 0)
  (hdeg_q_max : qPoly.natDegree < Fintype.card F)
  {t : ‚Ñï} (Q : MvPolynomial (Fin 2) F)
  (hdegX : MvPolynomial.degreeOf 0 Q < t)
  (hdegY : MvPolynomial.degreeOf 1 Q < qPoly.natDegree) :
  (MvPolynomial.eval‚ÇÇHom (Polynomial.C : F ‚Üí+* Polynomial F)
      (fun i : Fin 2 => if i = 0 then qPoly else Polynomial.X) Q).natDegree < t * qPoly.natDegree := by
  simp_all +decide [MvPolynomial.degreeOf_lt_iff]
  have h_deg_term : ‚àÄ m ‚àà Q.support, (m 0) * qPoly.natDegree + (m 1) < t * qPoly.natDegree := by
    intro m hm
    have := hdegX
    simp_all +decide [MvPolynomial.degreeOf_eq_sup]
    nlinarith [hdegY m hm,
      show m 0 < t from lt_of_le_of_lt
        (Finset.le_sup (f := fun m => m 0) (Finsupp.mem_support_iff.mpr hm)) hdegX]
  rw [MvPolynomial.eval‚ÇÇ_eq']
  have ht : 0 < t := Nat.pos_of_ne_zero (by omega)
  have hpos : (0 : ‚Ñï) < t * qPoly.natDegree := Nat.mul_pos ht hdeg_q_min
  refine lt_of_le_of_lt (Polynomial.natDegree_sum_le _ _)
    ((Finset.sup_lt_iff hpos).mpr ?_)
  intro m hm
  specialize h_deg_term m hm
  by_cases h : Q.coeff m = 0 <;>
    simp_all +decide [Polynomial.natDegree_C_mul]
  rw [Polynomial.natDegree_mul'] <;> aesop

/-- Definition 4.7
  `polyFold(f, k, r)` "folds" the polynomial `f`
  producing a new polynomial of deree  `< degree(f)/k`. -/
noncomputable def polyFold
  [DecidableEq F] (fPoly : Polynomial F)
  (k : ‚Ñï) (hk0 : 0 < k) (hkfin : k < Fintype.card F)
  (r : F) : Polynomial F :=
    let qPoly : Polynomial F := Polynomial.X ^ k
    let hdeg_q_min : qPoly.natDegree > 0 := by
      simp only [qPoly, Polynomial.natDegree_X_pow]; exact hk0
    let hdeg_q_max : qPoly.natDegree < Fintype.card F := by
      simp only [qPoly, Polynomial.natDegree_X_pow]; exact hkfin
  -- choose the unique bivariate lift Q
    let Q : MvPolynomial (Fin 2) F := polyQ fPoly qPoly
    MvPolynomial.eval‚ÇÇHom
      (Polynomial.C : F ‚Üí+* Polynomial F)
      (fun i : Fin 2 => if i = 0 then Polynomial.X else Polynomial.C r) Q

open Domain

variable {Œπ F : Type*} [Field F] [Fintype F] [DecidableEq F] [DecidableEq Œπ]

/-- Definition 4.8
  For x ‚àà Œπ·µè, p_x ‚àà ùîΩ[X] is the degree < k polynomial
  where p_x(y) = f(y) for every y ‚àà Œπ such that y·µè = x. -/
noncomputable def xPoly
  {S : Finset Œπ} (f : Œπ ‚Üí F) (œÜ : Œπ ‚Ü™ F) (k : ‚Ñï) (x : indexPow S œÜ k) : Polynomial F :=
  let dom := powFiber S œÜ k x
  let emb : { y // y ‚àà dom } ‚Üí F := œÜ ‚àò Subtype.val
  let g : { y // y ‚àà dom } ‚Üí F := f ‚àò Subtype.val
  Lagrange.interpolate univ emb g

/-- Definition 4.8
  Fold(f,k,Œ±) : Œπ·µè ‚Üí ùîΩ such that  Fold(f, k, Œ±)(x) := p_x(Œ±) -/
noncomputable def fold
  {S : Finset Œπ} (œÜ : Œπ ‚Ü™ F) (f : Œπ ‚Üí F) (k : ‚Ñï) (Œ± : F) : indexPow S œÜ k ‚Üí F :=
    fun x => (xPoly f œÜ k x).eval Œ±

/-- min{Œ¥·µ£(f, RSC[F, Œπ, degree]), 1 ‚àí B^‚ãÜ(œÅ)} -/
noncomputable def foldingDistRange
   (degree : ‚Ñï) [Fintype Œπ] [Nonempty Œπ] (œÜ : Œπ ‚Ü™ F) (f : Œπ ‚Üí F) : ‚Ñù‚â•0 :=
    let C : Set (Œπ ‚Üí F) := code œÜ degree
    letI : Nonempty C := by exact Zero.instNonempty
    letI : Fintype C := by exact Fintype.ofFinite ‚ÜëC
    min Œ¥·µ£'(f, C) (1 - Bstar (LinearCode.rate (code œÜ degree)))

open ProbabilityTheory

variable {Œπ F : Type} [Field F] [Fintype F] [DecidableEq F] [DecidableEq Œπ]

/-- Lemma 4.9
  For every function `f : Œπ ‚Üí F`, `degree`, folding parameter `k`, and
  `Œ¥ ‚àà (0, foldingDistRange)`
  `Pr_{r ‚Üê F} [ Œ¥·µ£(fold(f, k, Œ±), RS[F, Œπ·µè, degree/k)] < Œ¥] ‚â§ err'(degree/k, œÅ, Œ¥, k)` -/
lemma folding
  [Nonempty Œπ] {S : Finset Œπ} [Fintype Œπ]
  (œÜ : Œπ ‚Ü™ F) (f : Œπ ‚Üí F) (k : ‚Ñï)
  [Nonempty (indexPow S œÜ k)]
  {degree : ‚Ñï} (Œ¥ : ‚Ñù‚â•0) (hŒ¥Pos : Œ¥ > 0)
  (hŒ¥Lt : Œ¥ < foldingDistRange degree œÜ f) :
  let C : Set ((indexPow S œÜ k) ‚Üí F) := code (pow S œÜ k) (degree / k)
  Pr_{ let r ‚Üê $·µñ F }[ Œ¥·µ£((fold œÜ f k r), C) ‚â§ Œ¥]
    ‚â§ proximityError F (degree / k) (LinearCode.rate (code œÜ degree)) Œ¥ k :=
by sorry

end Folding
